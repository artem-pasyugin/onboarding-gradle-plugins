plugins {
    id 'java-gradle-plugin'
    id 'groovy'
}

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    api project(':base')

    // spring
    implementation 'org.springframework.boot:spring-boot-gradle-plugin:2.6.8'
    implementation 'io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE'

    testImplementation gradleTestKit()
    testImplementation 'com.netflix.nebula:nebula-test:10.2.0'
}

testing {
    suites {
        test {
            useSpock('2.1-groovy-3.0')

            dependencies {
                implementation project
            }
        }

        functionalTest(JvmTestSuite) {
            useSpock('2.1-groovy-3.0')

            dependencies {
                implementation project
                implementation('com.netflix.nebula:nebula-test:10.2.0')
            }

            targets {
                all {
                    testTask.configure { shouldRunAfter(test) }
                }
            }
        }
    }
}

gradlePlugin {
    plugins {
        create("microservice") {
            id = 'ru.onboarding.configure.microservice'
            implementationClass = 'ru.onboarding.configure.microservice.MicroservicePlugin'
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.test, sourceSets.functionalTest)

tasks.named('check') {
    dependsOn(testing.suites.functionalTest)
}