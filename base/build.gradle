plugins {
    id 'java-gradle-plugin'
    id 'groovy'
}

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    api 'com.fizzpod:gradle-gitignore-plugin:2.3.2'

    testImplementation gradleTestKit()
    testImplementation 'com.netflix.nebula:nebula-test:10.2.0'
}

testing {
    suites {
        test {
            useSpock('2.1-groovy-3.0')

            dependencies {
                implementation project
            }
        }

        functionalTest(JvmTestSuite) {
            useSpock('2.1-groovy-3.0')

            dependencies {
                implementation project
                implementation('com.netflix.nebula:nebula-test:10.2.0')
            }

            targets {
                all {
                    testTask.configure { shouldRunAfter(test) }
                }
            }
        }
    }
}

gradlePlugin {
    plugins {
        create("base") {
            id = "ru.onboarding.configure.base"
            implementationClass = "ru.onboarding.configure.base.BasePlugin"
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

gradlePlugin.testSourceSets(sourceSets.test, sourceSets.functionalTest)

tasks.named('check') {
    dependsOn(testing.suites.functionalTest)
}